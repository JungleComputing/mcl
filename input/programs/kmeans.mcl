/*
 * Copyright 2014 Pieter Hijma
 *
 * This file is part of MCL.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

module kmeans

import perfect;

perfect void
kmeans_kernel(int npoints,
	int nclusters,
	int nfeatures,
	float[nfeatures, npoints] points,   
	float[nclusters, nfeatures] clusters,
	int[npoints] pointsCluster) {

    foreach (int pid in npoints threads) {
		int ind = 0;
		float min_dist = 3.0e+38;
		for (int cluster = 0; cluster < nclusters; cluster++) {
		    float dist = 0;
		    for (int feature = 0; feature < nfeatures; feature++) {
				float d = (points[feature, pid] - clusters[cluster, feature]); // 1 FL op
				dist = dist + d * d;	// 2 FL ops
		    }
		    // 3 * nfeatures FL ops
		
		    if (dist < min_dist) {
				min_dist = dist;
				ind    = cluster;
		    }
		}
		// 3 * nfeatures * nclusters FL ops
		pointsCluster[pid] = ind;
    }
}

/*
INFO at |project://mcl/input/programs/kmeans.mcl|(45,13,<6,0>,<6,13>): computation:
  threads:
    instructions: 
      nclusters*npoints
    stores: 
      main: npoints
INFO at |project://mcl/input/programs/kmeans.mcl|(45,13,<6,0>,<6,13>): control flow:
  threads:
    loads: 
      main: 2*nclusters*nfeatures*npoints
    instructions: 
      5*nclusters*nfeatures*npoints+nclusters*npoints
INFO at |project://mcl/input/programs/kmeans.mcl|(45,13,<6,0>,<6,13>): Arithmetic intensity: nclusters

Translating to gpu and mic.
*/