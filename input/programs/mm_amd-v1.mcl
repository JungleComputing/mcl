module matrixmultiplication




import gpu;
import amd;




amd void matmul(const int n, const int m, const int p, global float[n,m] c, 
        const global float[n,p] a, const global float[p,m] b) {
        
        
    const int nrElsN = 16;
    const int nrBlocksN = n / nrElsN;

    const int nrThreadsM = gpu.hierarchy.blocks.block.threads.max_nr_units;
    const int nrBlocksM = m / nrThreadsM;

    const int nrLoadIters = p / nrThreadsM;
    
    a as float[nrBlocksN,nrLoadIters][nrElsN,nrThreadsM] a2;
    c as float[nrBlocksN,nrBlocksM][nrElsN,nrThreadsM] c2;
    
    foreach (const int bi in nrBlocksN workgroups) {
        foreach (const int bj in nrBlocksM workgroups) {

            local float[nrElsN][nrThreadsM] l_a;

            foreach (const int tj in nrThreadsM workitems) {
                reg float[nrElsN] sums;
                const int j = bj * nrThreadsM + tj;
                
                
                for (reg int ei = 0; ei < nrElsN; ei++) {
                    sums[ei] = 0.0;
                }
                
                
                for (reg int l = 0; l < nrLoadIters; l++) {
                	for (reg int ei = 0; ei < nrElsN; ei++) {
                        l_a[ei][tj] = a2[bi,l][ei,tj];
                    }
                
                
                	barrier(local);
                	
                	for (int k2 = 0; k2 < p/nrLoadIters; k2++) {
                		int k = l * p / nrLoadIters + k2;
                	
                		float bkj = b[k, j];
                	
                    	for (reg int ei = 0; ei < nrElsN; ei++) {
                        	sums[ei] += l_a[ei][k2] * bkj;
                    	}
                	}
                	barrier(local);
                }

                for (reg int ei = 0; ei < nrElsN; ei++) {
                    c2[bi,bj][ei,tj] += sums[ei];
                }
            }
        }
    }
}



/*

matmulKernel             : avg =   12 ms, total =   60 ms, count =         5

#GFLOPS: 1431 GFLOPS
Bandwidth: 3.9035 GB/s
Effective Bandwidth: 44.891 GB/s



*/
