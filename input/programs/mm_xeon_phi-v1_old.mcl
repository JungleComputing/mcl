/*
 * Copyright 2014 Pieter Hijma
 *
 * This file is part of MCL.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */



module matrixmultiplication


import perfect;
import xeon_phi;





xeon_phi void matmul(const int n = 2048, const int m = 2048, const int p = 2048, float[n,m] c, const 
        float[n,p] a, const float[p,m] b) {

	int nrElsM = 8;
	int nrElsN = 8;

    const int nrVectorsM = mic.hierarchy.threads.thread.vectors.nr_units;
    const int nrThreadsM = m / (1 * nrVectorsM * nrElsM);
    
    const int nrElsP = 16;
    const int nrBlocksP = p / nrElsP;
    
    int nrThreadsN = n / nrElsN;
    
    a as float[nrThreadsN, nrBlocksP][nrElsN, nrElsP] a2;
    b as float[nrBlocksP, nrThreadsM][nrElsP, nrElsM][1, nrVectorsM] b2;
    c as float[nrThreadsN, nrThreadsM][nrElsN, nrElsM][1, nrVectorsM] c2;
    
    foreach (const int ti in nrThreadsN threads) {
        foreach (const int tj in nrThreadsM threads) {
            foreach (const int vj in nrVectorsM vectors) {
                float[nrElsM][nrElsN] sums; 
                
            	for (int ej = 0; ej < nrElsM; ej++) {
	                for (int ei = 0; ei < nrElsN; ei++) {
                		sums[ej][ei] = 0.0;
                	}
                }
                
                for (int bk = 0; bk < nrBlocksP; bk++) {
            		for (int ej = 0; ej < nrElsM; ej++) {
            		
            			float[nrElsP] bTemp;
            			
            			for (int ek = 0; ek < nrElsP; ek++) {
            				bTemp[ek] = b2[bk, tj][ek, ej][0, vj];
            			}
            			
	            		for (int ei = 0; ei < nrElsN; ei++) {
	            			for (int ek = 0; ek < nrElsP; ek++) {
		                    	sums[ej][ei] = sums[ej][ei] + a2[ti, bk][ei, ek] * bTemp[ek];
		                    }
		                }
		            }
                }
                
                for (int ej = 0; ej < nrElsM; ej++) {
	                for (int ei = 0; ei < nrElsN; ei++) {
		           		c2[ti,tj][ei, ej][0, vj] = sums[ej][ei];
		           	}
		        }
            }
        }
    }
}